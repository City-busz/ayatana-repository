# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=libappindicator
pkgname=('libappindicator' 'libappindicator3')
pkgver=12.10.0
pkgrel=1
pkgdesc="Library to allow applications to export a menu into the Unity Menu bar"
arch=('i686' 'x86_64')
url="https://launchpad.net/libappindicator"
license=('LGPL')
makedepends=('libindicator' 'libindicator3' 'libdbusmenu-gtk2' 'libdbusmenu-gtk3' 'gobject-introspection' 'vala' 'gtk-sharp-2' 'pygtk' 'perl-xml-libxml' 'dbus-glib')
options=('!libtool')
source=(http://launchpad.net/libappindicator/${pkgver%.*}/$pkgver/+download/$pkgbase-$pkgver.tar.gz)
md5sums=('41910f2005edee9240da1e53fffcdc12')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  # Fix mono bindings location
  sed -i 's|/cli/|/mono/|' bindings/mono/{appindicator-sharp-0.1.pc.in,Makefile.in}

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --with-gtk=2
  make -j1
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --with-gtk=3
  make -j1
  popd
}

package_libappindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('libindicator' 'libdbusmenu-gtk2')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make DESTDIR="$pkgdir/" install
}

package_libappindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('libindicator3' 'libdbusmenu-gtk3')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make -C src DESTDIR="$pkgdir/" install
  make -C bindings/vala DESTDIR="$pkgdir/" install
}
