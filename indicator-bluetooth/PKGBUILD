# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-bluetooth
_pkgname=gnome-bluetooth
pkgver=3.6.1
pkgrel=1
pkgdesc="Bluetooth indicator for Unity"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('gnome-bluetooth' 'libappindicator3')
makedepends=('intltool' 'nautilus-sendto' 'gobject-introspection' 'yelp-tools' 'docbook-xsl' 'gtk-doc' 'gnome-common')
options=('!libtool' '!emptydirs')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz
        01_use_app_indicator.patch
        indicator-rename.patch)
sha256sums=('71d72a4637916450987f69c71d374c5bc1102be6dd9d9d7f923aa924659f768a'
            '1ad2713f6853189260e9ec29fe22d7d7983a70bc3c582a6782e7bbb8db2cc017'
            '5ec76dd9b56dc2d0b6432c56219473d64b3ad2513ff06f9aa732bcc2bc619b13')

build() {
  cd $_pkgname-$pkgver

  # Use application indicator
  patch -Np1 -i ../01_use_app_indicator.patch

  # Rename bluetooth-applet binary to avoid conflicts
  patch -Np1 -i ../indicator-rename.patch

  autoreconf -fi
  intltoolize --force
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd $_pkgname-$pkgver
  make -C applet DESTDIR="$pkgdir" install

  # Remove files that already in gnome-bluetooth
  rm -r "$pkgdir"/usr/{lib,share/gnome-bluetooth,share/man}
}
