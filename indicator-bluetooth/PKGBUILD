# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-bluetooth
pkgver=0.0.6
pkgrel=1
pkgdesc="Bluetooth indicator for Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-bluetooth"
license=('GPL')
depends=('gnome-bluetooth' 'libindicator3' 'libdbusmenu-gtk3' 'ido')
makedepends=('intltool' 'vala')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/13.04/$pkgver/+download/$pkgname-$pkgver.tar.xz)
md5sums=('af518cf32c3c5e1f0b44ed28a0a9994a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
