# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-bluetooth
pkgver=0.0.6
pkgrel=3
pkgdesc="Bluetooth indicator for Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-bluetooth"
license=('GPL')
depends=('gnome-bluetooth' 'libindicator3' 'libdbusmenu-gtk3' 'ido')
makedepends=('intltool' 'vala')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/13.04/$pkgver/+download/$pkgname-$pkgver.tar.xz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz
        remove-obexftp.patch)
md5sums=('af518cf32c3c5e1f0b44ed28a0a9994a'
         '291d81012439642a3099a1108879e1ae'
         '5d805fba186235dc2c5d3fc2b7542a49')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build with gnome-bluetooth 3.8
  patch -Np1 -i "$srcdir/remove-obexftp.patch"

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
