# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=evolution-indicator
pkgver=0.2.20
pkgrel=5
pkgdesc="A plugin for Evolution that uses the Messaging Menu to provide additional information to the shell about Evolution's state"
arch=('i686' 'x86_64')
url="https://launchpad.net/evolution-indicator"
license=('GPL')
depends=('evolution' 'indicator-messages' 'libunity' 'gconf')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz
        01_no_debug_spamming.patch
        02_evo_3_4_build_fixes.patch
        evolution-indicator-eds3.6_v2.patch
        add_copying_lgpl_license.patch
        autotools-1.13.patch)
md5sums=('9902c23b21bb8ea6c7ec42d58a2c6eb8'
         '8351b94c8c68fca96b23d7f24c824a5d'
         '7080b6e6f0913cfb9eb689e1973d6884'
         '60c9f29b06eee3181d85aca1c0faeebe'
         'd1bb465b6f5b2b6a732bb5f9c5852ff6'
         'efda684939abcfeec674378d2a72a11d'
         'e411cd1b14e6c564cb98c346bd0618af')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/01_no_debug_spamming.patch"
  patch -Np1 -i "$srcdir/02_evo_3_4_build_fixes.patch"
  patch -Np1 -i "$srcdir/add_copying_lgpl_license.patch"
  patch -Np1 -i "$srcdir/evolution-indicator-eds3.6_v2.patch"
  patch -Np1 -i "$srcdir/autotools-1.13.patch"

  sed -i 's|e-util/e-config.h|e-util/e-util.h|' src/evolution-indicator.c

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s " po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
