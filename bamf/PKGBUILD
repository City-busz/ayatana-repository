# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=bamf
pkgver=0.3.6
pkgrel=1
pkgdesc="Removes the headache of applications matching into a simple DBus daemon and c wrapper library"
arch=('i686' 'x86_64')
url="https://launchpad.net/bamf"
license=('GPL')
depends=('dbus-glib' 'libwnck3' 'libgtop')
makedepends=('gobject-introspection' 'vala')
options=('!libtool')
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        fix-current-desktop.patch)
md5sums=('56b0b0ac2d3f2a0401db268c78cc8527'
         '406dfbb06f78a325515186be83523b1b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build
  sed -i 's/ -Werror//' configure{,.in}
  sed -i 's/--c-include/--include/' lib/libbamf/Makefile.in

  # Fix desktop file table if XDG_CURRENT_DESKTOP environment variable is not available
  patch -Np1 -i "$srcdir/fix-current-desktop.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-webapps
  make
}

package() {
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install
}
