# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=unity-lens-video
pkgver=0.3.14
pkgrel=2
pkgdesc="Lens for Unity to search local and remote videos"
arch=('any')
url="https://launchpad.net/unity-lens-video"
license=('GPL')
depends=('zeitgeist' 'python2-gobject' 'unity-lens-music')
makedepends=('python2-distutils-extra')
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz)
md5sums=('e10137c4714a00fd8fa5d1e7e280fad3'
         '5168bf5ae719c9763b11066c3b960207')

build() {
  cd "$srcdir/${pkgname}s-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/unity-lens-video

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  python2 setup.py build
}

package() {
  cd "$srcdir/${pkgname}s-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1
}
