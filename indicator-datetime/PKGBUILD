# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-datetime
pkgver=12.10.2
pkgrel=1
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'ido' 'geoclue' 'evolution-data-server' 'dconf')
makedepends=('intltool')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=(http://launchpad.net/indicator-datetime/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120520.tar.gz)
md5sums=('7c8ffa6cf6fd224e21eb107c15c37602'
         'f974b52422ea714c4b79c38d6cc03b73')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install updated language files
  echo 'ace af am an ar ast az be bg bn bo bs ca ca@valencia crh cy cs da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fo fr fur fy gd gl gu gv he hi hr hu hy id is it ja jv ka kk kl km kn ko ku ky lb lt lv mg mhr mk ml mr ms my nb nds ne nl nn oc os pa pl pmy ps pt pt_BR ro ru sc sd shn si sk sl sq sr sv ta te th tr ug uk ur uz vec vi wae zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
