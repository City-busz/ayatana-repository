# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-datetime
pkgver=12.10.2
pkgrel=3
pkgdesc="A very, very simple clock"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-datetime"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'ido' 'evolution-data-server' 'dconf')
makedepends=('intltool' 'python2')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=(http://launchpad.net/indicator-datetime/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz
        systemd-support.patch)
md5sums=('7c8ffa6cf6fd224e21eb107c15c37602'
         '4f593f624fc6b9e76a6d3b66be6b4f37'
         '981e0e085ad20040917aef485f7ae79a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Add systemd support
  patch -Np1 -i "$srcdir/systemd-support.patch"

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --enable-systemd
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
