# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-power
pkgver=12.10.5
pkgrel=1
pkgdesc="An indicator to show the power status of your devices"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-power"
license=('GPL')
depends=('libindicator3' 'gnome-settings-daemon')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120520.tar.gz)
md5sums=('8949f336ee6d7d45ae16b28ba6888edd'
         '9a331432d3a0d070f5398881c6f8688f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  echo 'am an ar ast az be bg bn bo bs ca ca@valencia cy cs da de el en_AU en_CA en_GB eo es et eu fi fil fo fr fr_CA fy ga gd gl he hi hr hu id is it ja kk km ko ku lb lt lv mg mhr ms my nb nl nn oc os pa pl ps pt pt_BR ro ru sc sd shn si sk sl sq sr sv ta te th tr ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
