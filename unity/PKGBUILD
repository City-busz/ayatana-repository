# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=unity
pkgver=6.12.0
pkgrel=7
pkgdesc="Desktop experience designed for efficiency of space and interaction"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity"
license=('GPL')
depends=('compiz-devel' 'nux' 'bamf' 'libindicator3' 'libunity-misc' 'json-glib' 'libunity' 'libzeitgeist' 'unity-asset-pool' 'gnome-session' 'gnome-settings-daemon' 'gnome-settings-daemon-compat' 'gnome-themes-standard' 'gnome-screensaver' 'nautilus' 'polkit-gnome' 'indicator-session' 'unity-lens-applications')
makedepends=('cmake' 'libxslt' 'boost')
optdepends=('indicator-application: take menus from applications and place them in the panel'
            'indicator-appmenu: host the menus from an application'
            'indicator-bluetooth: Bluetooth menu'
            'indicator-datetime: a very, very simple clock'
            'indicator-messages: a place on the users desktop that collects messages that need a response'
            'indicator-power: show the power status of your devices'
            'indicator-printers: show active print jobs'
            'indicator-sound: a unified sound menu'
            'indicator-sync: aggregate services performing background synchronization'
            'unity-lens-files: exposing your files and file history'
            'unity-lens-gwibber: microblogging lens for Unity'
            'unity-lens-music: music, in the dash'
            'unity-lens-photos: all your photos, in the Dash'
            'unity-lens-shopping: gives you more suggestions when searching Unity'
            'unity-lens-video: lens for Unity to search local and remote videos'
            'unity-scope-gdocs: allows you to browse and search your Google Documents'
            'unity-scope-video-remote: Videos Lens scope for online sources')
install=unity.install
source=(http://launchpad.net/unity/6.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz
        807_806.diff
        806_805.diff
        2841_2840.diff
        disable-pointerbarrier.patch
        fix-build.patch
        3122_3121.diff
        launcher-add-timeout-remove.patch
        fix-workspace-switcher-icon.patch
        default-settings.patch
        unity.session
        unity.desktop
        unity.sh)
md5sums=('6a7f3d7f7629d1deb5f0662314c6022d'
         'fd72a0e890226defb3b10494370c9d08'
         '059ad61ceb92bc41a85696ceef665c93'
         '7b040fb255df3016f3981f1c4f6254b1'
         '21cefe15fbbbb0902f66845876a2bccf'
         '0a4606b775f0e6f514d77b95b1e0c408'
         '9ea2105ab95bc9f0c0d1933b9423453e'
         '35c68a372fa7eac459e66ee771bcb7d7'
         '45ef97186d889c9e5e96b0cd53af3302'
         '91d2e904fc35fee14164d5e133ab38ab'
         '7f1228776597800fc71a7eb6ef058e0d'
         '536c58f44a4a1bd5e314a0c15369298e'
         'fe17963c172e71d68704dfa4bb04bea4'
         '05abccd5c803b66a5e355130e3e91e6b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's/-Werror //' CMakeLists.txt

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/{unity.cmake,makebootchart.py}

  # Upstream fixes
  patch -Np0 -i "$srcdir/807_806.diff"
  patch -Np0 -i "$srcdir/806_805.diff"
  patch -Np0 -i "$srcdir/2841_2840.diff"

  # Disable PointerBarrier (not compatible yet with xserver 1.14)
  patch -Np1 -i "$srcdir/disable-pointerbarrier.patch"

  # Fix build
  patch -Np1 -i "$srcdir/fix-build.patch"

  # Use the new bluetooth-indicator
  patch -Np0 -i "$srcdir/3122_3121.diff"

  # Add timeout before remove a launcher
  patch -Np0 -i "$srcdir/launcher-add-timeout-remove.patch"

  # Fix workspace-switcher icon on launcher
  patch -Np1 -i "$srcdir/fix-workspace-switcher-icon.patch"

  # Set some default settings need to be adjusted
  patch -Np1 -i "$srcdir/default-settings.patch"

  # Use unity session instead of ubuntu
  sed -i '/COMPIZ_CONFIG_PROFILE/ s/ubuntu/unity/' tools/unity.cmake
  sed -i 's/Ubuntu Desktop/Unity Desktop/' panel/PanelMenuView.cpp

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  [[ -d build ]] || mkdir build
  cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DUSE_GSETTINGS=TRUE \
           -DCOMPIZ_BUILD_WITH_RPATH=FALSE \
           -DCOMPIZ_PACKAGING_ENABLED=TRUE \
           -DCOMPIZ_PLUGIN_INSTALL_TYPE=package \
           -DGETTEXT_FOUND=TRUE
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build"

  make DESTDIR="$pkgdir/" install

  # Replace distributor logo
  ln -sf ../../ccsm/icons/hicolor/64x64/apps/plugin-unityshell.png "$pkgdir/usr/share/unity/6/launcher_bfb.png" 

  # Install session and desktop files
  install -Dm644 "$srcdir/unity.session" "$pkgdir/usr/share/gnome-session/sessions/unity.session"
  install -Dm644 "$srcdir/unity.desktop" "$pkgdir/usr/share/xsessions/unity.desktop"

  # Install compiz profile configuration file
  install -Dm755 "$srcdir/unity.sh" "$pkgdir/etc/profile.d/unity.sh"
}
