# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-sound
pkgver=12.10.1
pkgrel=3
pkgdesc="A unified sound menu"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-sound"
license=('GPL')
depends=('libpulse' 'libindicator3' 'libdbusmenu-gtk3' 'ido' 'libnotify' 'libgee' 'dconf' 'xdg-utils')
makedepends=('intltool' 'vala')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/indicator-sound/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz)
md5sums=('d16b5c1bc530142d862ef95d3b4ed963'
         '742685a665cfe980c6f5d73b86afe0db')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Fix icon names to provide the playlist icon
  install -dm755 "$pkgdir"/usr/share/libindicator/icons/hicolor/{16x16,scalable}/mimetypes
  mv "$pkgdir"/usr/share/libindicator/icons/hicolor/16x16/status/sound-icon.png \
     "$pkgdir"/usr/share/libindicator/icons/hicolor/16x16/mimetypes/playlist.png
  mv "$pkgdir"/usr/share/libindicator/icons/hicolor/scalable/status/sound-icon.svg \
     "$pkgdir"/usr/share/libindicator/icons/hicolor/scalable/mimetypes/playlist.svg
  rm -r "$pkgdir"/usr/share/libindicator/icons/hicolor/{16x16,scalable}/status
}
