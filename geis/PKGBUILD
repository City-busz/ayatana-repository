# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=geis
pkgver=2.2.14
pkgrel=1
pkgdesc="Gesture Recognition And Instantiation Library"
arch=('i686' 'x86_64')
url="https://launchpad.net/geis"
license=('LGPL')
depends=('dbus-core' 'grail' 'python2')
makedepends=('xorg-server-devel')
options=('!libtool' '!emptydirs')
source=(https://launchpad.net/geis/trunk/$pkgver/+download/$pkgname-$pkgver.tar.xz)
md5sums=('a75f07d6d6333a591b5d1060f3b1cdcf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python2 fix
  sed -i 's/python >= 2.7/python-2.7 >= 2.7/' configure
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/geisview/{__init__.py,geisview} python/pygeis

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
