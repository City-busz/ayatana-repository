# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=unity-lens-music
pkgver=6.8.1
pkgrel=2
pkgdesc="Music, in the dash"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-music"
license=('GPL')
depends=('json-glib' 'libunity' 'tdb' 'gstreamer0.10-base-plugins')
makedepends=('vala' 'intltool')
source=(http://launchpad.net/$pkgname/6.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz)
md5sums=('6a14bdc6dfbddf24f9bebe6575f687ec'
         'e48d8ad4a89000aea09d40f1fd89ed79')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
