# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-session
pkgver=12.10.4
pkgrel=2
pkgdesc="Indicator showing session management, status and user switching"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'dconf')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/indicator-session/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        remove-unused-deps.patch
        gdbus-port.patch
        fix-fallback-icon.patch
        support-reboot.patch
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120520.tar.gz)
md5sums=('8d9e3a108c9542b36c0f7e8cd1f07c19'
         '960b2c9b0d5a701750bcc5fe56ccece3'
         '295da6bb87f47c278a64a5aa3d5c23d1'
         '9922b515df980c6240633b2a97af78fe'
         '202a619c2b59b6079f60302863de7bdc'
         '8b13bb185b342ce9721efcce6f7f088c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install updated language files
  echo 'ace af am an ar ast az be bg bn bo br bs ca ca@valencia crh cv cy cs csb da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fo fr fur fy gd gl gu gv he hi hr hu hy id is it ja ka kk km kn ko ku ky lb lt lv mg mhr mk ml mr ms my nb ne nl nn ny oc os pa pl ps pt pt_BR ro ru sc sd shn si sk sl sq sr sv ta ta_LK te th tr tt ug uk ur uz vec vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  # Upstream patches
  # https://bugs.launchpad.net/indicator-session/+bug/1085100
  patch -Np0 -i "$srcdir/remove-unused-deps.patch"
  patch -Np0 -i "$srcdir/gdbus-port.patch"

  # Other fixes
  # https://code.launchpad.net/~ballogy/indicator-session/better-fallback-icon/+merge/141608
  patch -Np0 -i "$srcdir/fix-fallback-icon.patch"

  # https://code.launchpad.net/~ballogy/indicator-session/gnome-session-reboot/+merge/141611
  patch -Np1 -i "$srcdir/support-reboot.patch"

  sed -i 's/gnome-control-center credentials/credentials-preferences/' src/online-accounts-mgr.c
  sed -i 's/Ubuntu Help/Desktop Help/' src/session-menu-mgr.c

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --disable-gtklogouthelper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Remove guest session
  rm -r "$pkgdir/usr/share/indicators"
}
