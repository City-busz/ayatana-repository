# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-session
pkgver=12.10.4
pkgrel=3
pkgdesc="Indicator showing session management, status and user switching"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-session"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'dconf')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/indicator-session/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz
        remove-unused-deps.patch
        gdbus-port.patch
        fix-build.patch
        fix-fallback-icon.patch
        support-reboot.patch
        systemd-support.patch)
md5sums=('8d9e3a108c9542b36c0f7e8cd1f07c19'
         '45dcf73e2937395dbc2ea452d71d531f'
         '960b2c9b0d5a701750bcc5fe56ccece3'
         '295da6bb87f47c278a64a5aa3d5c23d1'
         'd735ecef7c9425a2653e1a7bab311435'
         '9922b515df980c6240633b2a97af78fe'
         '202a619c2b59b6079f60302863de7bdc'
         '87c3a3b2591565bc84c8e52b4cd95168')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  # Upstream patches
  # https://bugs.launchpad.net/indicator-session/+bug/1085100
  patch -Np0 -i "$srcdir/remove-unused-deps.patch"
  patch -Np0 -i "$srcdir/gdbus-port.patch"

  # Fix build
  patch -Np1 -i "$srcdir/fix-build.patch"

  # Other fixes
  # https://code.launchpad.net/~ballogy/indicator-session/better-fallback-icon/+merge/141608
  patch -Np0 -i "$srcdir/fix-fallback-icon.patch"

  # https://code.launchpad.net/~ballogy/indicator-session/gnome-session-reboot/+merge/141611
  patch -Np1 -i "$srcdir/support-reboot.patch"

  # https://code.launchpad.net/~ballogy/indicator-session/systemd-support/+merge/141656
  patch -Np0 -i "$srcdir/systemd-support.patch"

  sed -i 's/gnome-control-center credentials/credentials-preferences/' src/online-accounts-mgr.c
  sed -i 's/Ubuntu Help/Desktop Help/' src/session-menu-mgr.c

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --disable-gtklogouthelper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Remove guest session
  rm -r "$pkgdir/usr/share/indicators"
}
