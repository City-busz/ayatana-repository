# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-printers
pkgver=0.1.6
pkgrel=2
pkgdesc="An indicator which shows active print jobs"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-printers"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'cups')
makedepends=('intltool' 'python2')
options=('!libtool')
source=(http://launchpad.net/$pkgname/0.1/$pkgver/+download/$pkgname-$pkgver.tar.gz
        fix-build.patch
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120520.tar.gz)
md5sums=('a6c7727f7f862e99fd849750b4b5baf8'
         '43e3acfe455cf8370e15491123e52403'
         '3a6e197f76e7269cc3bfd394aac09493')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build
  patch -Np0 -i "$srcdir/fix-build.patch"

  # Install language files
  echo 'ar ast az be bg bn bs ca ca@valencia cs da de el en_AU en_GB eo es et eu fi fr fr_CA fy gd gl he hi hr hu id is it ja kk km ko ku lt lv ms my nb nl nn oc os pl pt pt_BR ro ru sd si sk sl sq sr sv ta te tr ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
