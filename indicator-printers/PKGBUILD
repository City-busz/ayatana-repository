# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-printers
pkgver=0.1.6
pkgrel=3
pkgdesc="An indicator which shows active print jobs"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-printers"
license=('GPL')
depends=('libindicator3' 'libdbusmenu-gtk3' 'cups')
makedepends=('intltool' 'python2')
options=('!libtool')
source=(http://launchpad.net/$pkgname/0.1/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz
        fix-build.patch)
md5sums=('a6c7727f7f862e99fd849750b4b5baf8'
         '3340be69c7131d94f9019f5308c7a780'
         '43e3acfe455cf8370e15491123e52403')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build
  patch -Np0 -i "$srcdir/fix-build.patch"

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
