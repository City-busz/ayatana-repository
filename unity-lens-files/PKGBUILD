# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=unity-lens-files
pkgver=6.6.1
pkgrel=2
pkgdesc="Daemon exposing your files and file history to Unity"
arch=('i686' 'x86_64')
url="https://launchpad.net/unity-lens-files"
license=('GPL')
depends=('libzeitgeist' 'libunity' 'dconf')
makedepends=('vala' 'intltool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/6.0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz)
md5sums=('f97c08d40288e048cb6be9f1dedc468f'
         '85de25db7c37a24179d5bc74632f4338')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
