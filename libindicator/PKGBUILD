# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=libindicator
pkgname=('libindicator' 'libindicator3')
pkgver=12.10.1
pkgrel=1
pkgdesc="Libary with a set of symbols and convience functions that all indicators would like to use"
arch=('i686' 'x86_64')
url="https://launchpad.net/libindicator"
license=('GPL')
makedepends=('gtk2' 'gtk3')
options=('!libtool')
source=(http://launchpad.net/libindicator/${pkgver%.*}/$pkgver/+download/$pkgbase-$pkgver.tar.gz)
md5sums=('5fd4d6bab339fc9611078b64c44a85a8')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  # Fix build
  sed -i 's/-Werror//' */Makefile.{am,in}

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgbase \
              --disable-static --with-gtk=2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgbase \
              --disable-static
  make
  popd
}

package_libindicator() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make -j1 DESTDIR="$pkgdir/" install
}

package_libindicator3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3')

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make -j1 -C libindicator DESTDIR="$pkgdir/" install
  make -C tools DESTDIR="$pkgdir/" install

  rm -r "$pkgdir/usr/share"
}
