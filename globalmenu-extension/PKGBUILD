# Maintainer: György Balló <ballogy@freestart.hu>
# Contributor: Tom Kuther <gimpel@sonnenkinder.org>

pkgbase=globalmenu-extension
pkgname=('firefox-globalmenu' 'thunderbird-globalmenu')
pkgver=3.7.2
pkgrel=1
arch=('i686' 'x86_64')
url="https://launchpad.net/globalmenu-extension"
license=('GPL' 'LGPL' 'MPL')
makedepends=('libdbusmenu-gtk2' 'autoconf2.13' 'yasm' 'xulrunner' 'zip' 'unzip')
source=(http://pkgbuild.com/~bgyorgy/sources/$pkgbase-$pkgver.tar.gz)
md5sums=('c430062d0a923b2b8f6b3829150f7a96')

build() {
  cd "$srcdir/~extension-hackers/$pkgbase/${pkgver%.*}"

  autoconf-2.13
  ./configure --enable-application=extensions \
              --enable-extensions=globalmenu \
              --with-libxul-sdk=`pkg-config --variable=sdkdir libxul` \
              --with-system-libxul \
              --with-system-nspr \
              --with-system-nss \
              --disable-webrtc \
              --disable-tests
  make
}

package_firefox-globalmenu() {
  pkgdesc="Firefox extension that exports the standard menu bar via libdbusmenu"
  depends=('libdbusmenu-gtk2' 'firefox')

  cd "$srcdir/~extension-hackers/$pkgbase/${pkgver%.*}"
  emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' dist/xpi-stage/globalmenu/install.rdf)
  install -d "$pkgdir/usr/lib/firefox/extensions/$emid"
  unzip -d "$pkgdir/usr/lib/firefox/extensions/$emid" dist/xpi-stage/globalmenu.xpi
}

package_thunderbird-globalmenu() {
  pkgdesc="Thunderbird extension that exports the standard menu bar via libdbusmenu"
  depends=('libdbusmenu-gtk2' 'thunderbird')

  cd "$srcdir/~extension-hackers/$pkgbase/${pkgver%.*}"
  emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' dist/xpi-stage/globalmenu/install.rdf)
  install -d "$pkgdir/usr/lib/thunderbird/extensions/$emid"
  unzip -d "$pkgdir/usr/lib/thunderbird/extensions/$emid" dist/xpi-stage/globalmenu.xpi
}
