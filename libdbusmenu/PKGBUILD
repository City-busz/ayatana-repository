# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=libdbusmenu
pkgname=('libdbusmenu' 'libdbusmenu-gtk2' 'libdbusmenu-gtk3')
pkgver=12.10.2
pkgrel=1
pkgdesc="Small little library that passes a menu structure across DBus"
arch=('i686' 'x86_64')
url="https://launchpad.net/dbusmenu"
license=('LGPL')
makedepends=('gtk2' 'gtk3' 'intltool' 'gnome-doc-utils' 'gobject-introspection' 'vala' 'python2' 'json-glib')
options=('!libtool' '!emptydirs')
source=(http://launchpad.net/dbusmenu/${pkgver%.*}/$pkgver/+download/$pkgbase-$pkgver.tar.gz)
md5sums=('e30fc986b447f62513d61225fa573a70')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' tools/dbusmenu-bench

  # Fix build
  sed -i 's/-Werror//' */Makefile.{am,in} */*/Makefile.{am,in}

  [[ -d build-gtk2 ]] || mkdir build-gtk2
  pushd build-gtk2
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-static --with-gtk=2
  make
  popd

  [[ -d build-gtk3 ]] || mkdir build-gtk3
  pushd build-gtk3
  ../configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-static
  make
  popd
}

package_libdbusmenu() {
  depends=('glib2')
  optdepends=('python2: for dbusmenu-dumper tool'
              'gtk2: for dbusmenu-bench tool'
              'json-glib: for dbusmenu-testapp tool')
  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make -C libdbusmenu-glib DESTDIR="$pkgdir/" install
  make -C tools DESTDIR="$pkgdir/" install
  make -C docs DESTDIR="$pkgdir/" install
}

package_libdbusmenu-gtk2() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2' "libdbusmenu=$pkgver")

  cd "$srcdir/$pkgbase-$pkgver/build-gtk2"

  make -C libdbusmenu-glib DESTDIR="$pkgdir/" install
  make -C libdbusmenu-gtk DESTDIR="$pkgdir/" install
  make -C libdbusmenu-glib DESTDIR="$pkgdir/" uninstall
}

package_libdbusmenu-gtk3(){
  pkgdesc+=" (GTK+ 3 library)"
  depends=('gtk3' "libdbusmenu=$pkgver")

  cd "$srcdir/$pkgbase-$pkgver/build-gtk3"

  make -C libdbusmenu-glib DESTDIR="$pkgdir/" install
  make -C libdbusmenu-gtk DESTDIR="$pkgdir/" install
  make -C libdbusmenu-glib DESTDIR="$pkgdir/" uninstall
}
