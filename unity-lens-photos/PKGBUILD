# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=unity-lens-photos
pkgver=0.9
pkgrel=3
pkgdesc="All your photos, in the Dash"
arch=('any')
url="https://launchpad.net/unity-lens-photos"
license=('GPL')
depends=('python-gobject' 'libunity' 'libgdata' 'libsoup-gnome' 'account-plugins' 'python-oauthlib')
makedepends=('python-distutils-extra')
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz)
md5sums=('8c57e46debe367909c24d27df9ef9f70'
         '79744c76bb0687cad5382cf0d16a012f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python3@' src/*

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  python3 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python3 setup.py install --root=$pkgdir/ --optimize=1
}
