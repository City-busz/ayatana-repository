# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=unity-lens-photos
pkgver=0.9
pkgrel=1
pkgdesc="All your photos, in the Dash"
arch=('any')
url="https://launchpad.net/unity-lens-photos"
license=('GPL')
depends=('python-gobject' 'libunity' 'libgdata' 'libsoup-gnome' 'account-plugins' 'python-httplib2')
makedepends=('python-distutils-extra')
source=(http://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        51_50.diff)
md5sums=('8c57e46debe367909c24d27df9ef9f70'
         'bc0915b30005372427fbd753549fe8d6')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python3@' src/*

  # Revert oauthlib usage
  patch -RNp0 -i "$srcdir/51_50.diff"

  python3 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python3 setup.py install --root=$pkgdir/ --optimize=1
}
