# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-messages
pkgver=12.10.5
pkgrel=2
pkgdesc="A place on the user's desktop that collects messages that need a response"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-messages"
license=('GPL')
depends=('libindicator3' 'dconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/indicator-messages/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20130310.tar.gz)
md5sums=('e7d6da8e9a88d9c45a8ff80af07eb958'
         '25eb65940da40b2fd50f96b845c4000c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build
  sed -i 's/-Werror//' */Makefile.{am,in}

  # Install updated language files
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*
  printf "%s\n" po/*.po | sed -e 's/po\///g' -e 's/\.po//g' >po/LINGUAS

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
