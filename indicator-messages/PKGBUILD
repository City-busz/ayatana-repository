# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=indicator-messages
pkgver=12.10.5
pkgrel=1
pkgdesc="A place on the user's desktop that collects messages that need a response"
arch=('i686' 'x86_64')
url="https://launchpad.net/indicator-messages"
license=('GPL')
depends=('libindicator3' 'dconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/indicator-messages/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120520.tar.gz)
md5sums=('e7d6da8e9a88d9c45a8ff80af07eb958'
         '98f6e80cb42a539bd8562d9e3aa95a30')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Install language files
  echo 'ace af am an ar ast az be bem bg bn bo br bs ca ca@valencia crh cv cy cs csb da de dv el en_AU en_CA en_GB eo es et eu fa fi fil fo fr fur fy ga gd gl gu guc gv he hi hr ht hu hy id is it ja ka kk km kn ko ku ky lb lt ltg lv mg mhr mi mk ml mr ms my nan nb ne nl nn nso ny oc or os pa pl ps pt pt_BR ro ru sc sd shn si sk sl sq sr sv sw ta ta_LK te th tr tt ug uk ur uz vec vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
